plugins {
//    id 'nebula.lint' version '9.3.2'
    id "com.palantir.idea-test-fix" version "0.1.0"
    id 'groovy'
    id 'maven-publish'
    id 'idea'
    id 'java'
    id "com.gradle.plugin-publish" version "0.15.0"
    id "java-gradle-plugin"
    id 'com.palantir.git-version' version '0.12.0-rc2'
    id 'com.adarshr.test-logger' version '3.0.0'
}

group 'jp.co.soramitsu'
def pluginId = "${group}.sora-plugin"
version System.getenv('TAG_NAME') ?: "0.4.0"

println("Version is")
println project.version

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation gradleTestKit()

    implementation 'com.bmuschko:gradle-docker-plugin:6.7.0'
    implementation 'gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.12.0-rc2'

    testImplementation('org.spockframework:spock-core:2.0-groovy-3.0') {
        exclude module: 'groovy-all'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Sora plugin',
                'Implementation-Version': archiveVersion,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

gradlePlugin {
    plugins {
        soraPlugin {
            id = pluginId
            displayName = "Gradle Sora plugin"
            implementationClass = 'jp.co.soramitsu.devops.SoraPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/soramitsu/gradle-sora-plugin'
    vcsUrl = 'https://github.com/soramitsu/gradle-sora-plugin'
    description = 'Gradle plugin that provides tasks to unify tasks for gradle projects across Soramitsu projects.'
    tags = ['soramitsu', 'gradle']
}

test {
    useJUnitPlatform()
    def projectsDir = "$project.projectDir/projects"
    inputs.files fileTree(dir: projectsDir, exclude: '**/.gradle')
}

test.dependsOn(publishToMavenLocal)

testlogger {
    theme 'standard'
    showExceptions true
    slowThreshold 2000
    showSummary true
    showPassed true
    showSkipped true
    showFailed true
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
}
