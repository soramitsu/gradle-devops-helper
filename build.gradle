plugins {
//    id 'nebula.lint' version '9.3.2'
    id "com.palantir.idea-test-fix" version "0.1.0"
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'idea'
    id 'java'
    id "com.gradle.plugin-publish" version "0.10.1"
    id "java-gradle-plugin"
    id 'com.palantir.git-version' version '0.12.0-rc2'
    id 'com.adarshr.test-logger' version '1.6.0'
}

group 'jp.co.soramitsu'
def pluginId = "${group}.sora-plugin"
version System.getenv('PLUGIN_VERSION') ?: "0.1.3"

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation gradleTestKit()

    implementation 'com.bmuschko:gradle-docker-plugin:4.6.0'
    implementation 'gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.12.0-rc2'


    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Sora plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

gradlePlugin {
    plugins {
        soraPlugin {
            id = pluginId
            displayName = "Gradle Sora plugin"
            implementationClass = 'jp.co.soramitsu.devops.SoraPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/soramitsu/gradle-sora-plugin'
    vcsUrl = 'https://github.com/soramitsu/gradle-sora-plugin'
    description = 'Gradle plugin that provides tasks to unify tasks for gradle projects across Soramitsu projects.'
    tags = ['soramitsu', 'gradle']
}

test {
    def projectsDir = "$project.projectDir/projects"
    inputs.files fileTree(dir: projectsDir, exclude: '**/.gradle')
}

test.dependsOn(install)

testlogger {
    theme 'standard'
    showExceptions true
    slowThreshold 2000
    showSummary true
    showPassed true
    showSkipped true
    showFailed true
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
}
